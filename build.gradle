buildscript {
    repositories {
        mavenCentral()
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = project.modVersion
group = project.modGroup // http://maven.apache.org/guides/mini/guide-naming-conventions.html

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}


repositories {
    maven {
        url 'https://nexus.seedfinding.com/repository/maven-seedfinding'
    }
    maven {
        url 'https://nexus.seedfinding.com/repository/maven-seedfinding-snapshots'
    }
    maven {
        url 'https://mod-buildcraft.com/maven/'
    }
    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
    maven { url = 'https://repo.spongepowered.org/maven/' }
    maven { url = 'https://jitpack.io' }
    mavenCentral()
}

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')

            property 'fml.coreMods.load', 'com.Mod.mixin.SeedModMixinLoader'

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    jarLibs
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:$minecraftVersion-$forgeVersion"

    jarLibs('org.spongepowered:mixin:0.8.2') {
        exclude module: 'commons-io'
        exclude module: 'gson'
        exclude module: 'guava'
    }
    jarLibs  "com.github.ZeroMemes:Alpine:1.5"

    // Hacky way to get mixins to work
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor') {
        exclude module: 'gson'
    }
    jarLibs ('com.seedfinding:mc_terrain:9e937ddb838e28e79423c287fa18b1ce66f061d7')
    jarLibs ('com.seedfinding:mc_biome:533cdb5ed955ff1c3106f9901aea6aa5b08fd781')
    jarLibs('com.seedfinding:mc_seed:5518e3ba3ee567fb0b51c15958967f70a6a19e02')
    jarLibs('com.seedfinding:mc_core:d685a37f0a466d4bf800e7f285daf9ef73c81678')

    implementation configurations.jarLibs
}

mixin {
    defaultObfuscationEnv 'searge'
    sourceSets {
        main {
            ext.refMap = 'mixins.seedmod.refmap.json'
        }
    }
}

processResources {
    exclude '**/rawimagefiles'

    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        include 'mcmod.info'
        expand version: version, 'mcversion': minecraftVersion
    }
}

task sourceJar(type: Jar) { // Generate sources
    group 'build'
    description 'Assemble API library source archive'

    classifier 'api-source'
    from sourceSets.main.allSource
}

task apiJar(type: Jar) {
    group 'build'
    description 'Assemble API library archive'

    classifier 'api'
    from sourceSets.main.output
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes(
                'Manifest-Version': 1.0,
                'MixinConfigs': 'mixins.seedmod.json',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': 'com.Mod.mixin.SeedModMixinLoader',
                'ForceLoadAsMod': 'true'
        )
    }
    from {
        exclude '**/module-info.class',
                'DebugProbesKt.bin',
                'META-INF/proguard/**',
                'META-INF/versions/**',
                'META-INF/**.RSA',
                'META-INF/com.android.tools/**',
                'META-INF/*.kotlin_module',
                'kotlin/**/*.kotlin_metadata',
                'kotlin/**/*.kotlin_builtins',
                'META-INF/*.version'
        configurations.jarLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task buildApi {
    group 'build'
    dependsOn sourceJar
    dependsOn apiJar
    description 'Assemble API library archives'
}
